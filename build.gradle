plugins {
    id 'java'
    id 'application'
    id 'java-library'
    id 'com.google.protobuf' version '0.8.17'
}

repositories {
    mavenCentral()
}

java{
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

mainClassName = "JECDAR"



dependencies {
    testImplementation "junit:junit:4.12"
    implementation 'io.grpc:grpc-netty-shaded:1.42.0'
    implementation 'io.grpc:grpc-protobuf:1.42.0'
    implementation 'io.grpc:grpc-stub:1.42.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'org.jdom:jdom2:2.0.6'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation files('lib/jcdd.jar')
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.17.3"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.42.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        proto{
            srcDirs = ['src/proto']
        }
    }

    test {
        java {
            srcDirs = ['test']
        }
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes(
                'Main-Class': 'my.project.main',
        )
    }
    classifier = 'fat'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

task submodulesUpdate(type:Exec) {
    description 'Updates (and inits) git submodules'
    commandLine 'git', 'submodule', 'update', '--init', '--recursive'
    group 'Build Setup'
}